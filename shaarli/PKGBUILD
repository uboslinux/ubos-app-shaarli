developer=https://github.com/shaarli
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.12.2
pkgrel=1
pkgdesc="Personal, minimalist, super-fast, bookmarking service"
arch=('any')
license=("custom:${pkgname}")
source=("https://github.com/shaarli/Shaarli/releases/download/v${pkgver}/shaarli-v${pkgver}-full.tar.gz")
depends=('perl-digest-sha1') # This dependency is only needed until
                             # https://github.com/uboslinux/ubos-admin/issues/594
                             # is fixed
releasepage=('https://github.com/shaarli/Shaarli/releases')
options=('!strip')
sha512sums=('be95e1d4d38a08da5469844de02f43a46ef18470194649a03714c9cb8fffe2dcd9052a80eef3bda90bc5c2de4ae323c9b81c8b15048ea3e19d46f1af6b662440')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# License
    install -D -m0644 ${startdir}/COPYING -t ${pkgdir}/ubos/share/licenses/${pkgname}/

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Templates
    install -D -m0644 ${startdir}/tmpl/deny-htaccess -t ${pkgdir}/ubos/share/${pkgname}/tmpl/
    install -D -m0644 ${startdir}/tmpl/datastore.php -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

    rsync --quiet --archive --exclude '.git*' ${startdir}/src/Shaarli/{*,.htaccess} ${pkgdir}/ubos/share/${pkgname}/web

# License
    install -D -m0644 ${srcdir}/Shaarli/COPYING -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
